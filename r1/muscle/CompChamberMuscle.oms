begin # blocking for automatic include

#begin##
$begin CompChamberMuscle$$
$spell
	Fe
	Tg
	Yg
	Tf
$$

$section Compartmental Model for O2 Concentration in the Chamber or on the Muscle Surface$$

$table
$bold Syntax$$ $cnext
$syntax/[/Psi/, /Psi_a/, /sigma/] = CompChamberMuscle( ...
	/Fe/, /Vc/, /Tg/, /Yg/, /theta/, /a/ )/$$
$tend

$fend 20$$

$head Notation$$
$table	
$latex t$$		$cnext time                                  		$cnext time   		$rnext
$latex C_c (t)$$	$cnext Concentration of O2 in the chamber    		$cnext mass per volume 	$rnext
$latex C_0$$		$cnext Initial concentration of O2 in the chamber	$cnext mass per volume	$rnext
$latex F (a, t)$$	$cnext Muscle O2 consumption rate above the baseline 	$cnext mass per volume 	$rnext
$latex Fb$$		$cnext Muscle O2 consumption rate at baseline		$cnext mass per time	$rnext
$latex Fe$$		$cnext Electrode O2 consumption rate			$cnext mass per time	$rnext
$latex Vc$$		$cnext Volume of the chamber				$cnext length cubed	$rnext
$tend

$head Description$$
The model calculate the value of $latex C_c (t)$$ for a grid of values of $latex t$$ 
specified by the column vector $italic Tg$$ where $latex C_c (t)$$ satisfies the 
differential equations:

$latex \[
\frac{d C_c}{d t }(t) =
- \frac{ Fe + Fb * \exp[ F(a,t)]}{ Vc }
\]
$$

with the following initial conditions:
$latex \[
C_c ( t=0 )  = C_0 
\] $$

$head F(a, t)$$
Is the unknown function $latex F(a,t)$$ representing the muscle O2 consumption
rate during stimulation above the baseline consumption rate. It is expressed
as an expansion of the form:
$latex \[
F(a, t)  =  \sum_{j=1}^N a_j 
	\sqrt{ \frac{ 2 }{ T_f } } 
	\sin \left[ \pi \left( j - \frac{1}{2} \right)  \frac{ t }{ T_f } \right]
\] $$


$head N$$
Is the number of elements in (length of) the column vector $italic a$$.


$head Tf$$
Is the final time point for the simulation; i.e., is the last element of the
time grid $italic Tg$$ .


$head Fe$$
Is a double precision scalar specifying the rate of O2 consumption by 
the electrode in the chamber (mass per time).


$head Vc$$
Is a double precision scalar specifying the volume of the chamber
(units of length cubed).


$head Tg$$
Is a double precision column vector specifying the times at which
measurements were made.


$head Yg$$
Is a double precision column vector (same length as $italic Tg$$) with 
$th i$$ element equal to the measured value at $th i$$ element of 
$italic Tg$$. The value $latex C_c ( Tg_i )$$ is the model for 
$latex Yg_i$$.  


$head theta$$
Is a double precision column vector specifying the value of $latex \theta$$:
$latex C_0 = \theta_2$$ and $latex F_b = \theta_3$$ (note $latex \theta_1$$ is 
reserved for special use by $code SmoothSpline$$).


$head a$$
Is a double precision column vector of length $latex N$$ specifying the amplitudes
in the definition of $xref/CompChamberMuscle/F(a, t)/F(a, t)/$$.


$head Psi$$
If the number of output arguments is one or two ($latex 1 \leq nargout \leq 2 $$), 
the column vector $italic Psi$$ has the same number of elements as the time grid 
column vector $italic Tg$$, and $latex Psi_i = C_c [ Tg_i ]$$. It is the mean
(model) for the measurement $latex Yg$$, given the amplitude $italic a$$.


$head Psi_a$$
If the number of output arguments is two ($latex nargout = 2 $$), the matrix 
$italic Psi_a$$ has the same number of rows as the vector $italic Tg$$ and same 
number of columns as the vector $italic a$$. The elements of $italic Psi_a$$ are 
given by the formula:
$latex \[
Psi_a ( i , j ) = \frac{ \partial }{ \partial a_j } C_c [ Tg_i ]
\] $$


$head sigma$$
If the number of output arguments is three ($latex nargout = 3 $$), the column 
vector $italic sigma$$ has the same number of elements as the vector $italic Tg$$.
The $th i$$ element of $italic sigma$$ is the standard deviation of the 
measurement at time $italic Tg_i$$.

$end
---------------------------------------------------------------------------
#end##

local function [Fval, Fval_a] = F(Tf, a, t) begin
	N      = rowdim(a)
	angle  = (PI * t / Tf) * (seq(N) - 0.5d0)'
	Fval   = exp( sqrt(2d0 / Tf) * (sin(angle) * a ) )
	Fval_a = fillcols(Fval , N) % ( sqrt(2d0 / Tf) % sin(angle) )
end

local function RHS(Fe, Fb, Vc, Tf, a, t) begin
	[Fval, Fval_a] = F(Tf, a, t)

	return - ( Fe + Fb * Fval) / Vc
end

local function RHS_a(Fe, Fb, Vc, Tf, a, t) begin
	[Fval, Fval_a] = F(Tf, a, t)
	return - Fb * Fval_a / Vc
end

function [Psi, Psi_a, sigma] = CompChamberMuscle( ...
	Fe, Vc, Tg, Yg, theta, a ) begin
	#
	if nargout == 3 then begin
		sigma = 1d-4 * Yg
		return
	end
	#
	# dimension the return value
	Psi = fill(0d0, rowdim(Tg), 1)

	#
	C0    = theta(2)               # initial O2 concentration in the chamber
	Fb    = theta(3)               # baseline rate of O2 consumption by the muscle
	Tf    = Tg(end)                # time corresponding to last data point
	M     = rowdim(Tg)             # number of measruements
	bound = 1d1                    # fith order approximation over intervals of this size
	#
	t0    = 0d0
	C     = C0
	for i = 1 to M begin
		function RHS1 = RHS(Fe, Fb, Vc, Tf, a)
		C = C + gaussq(function RHS1, t0, Tg(i), bound)
		Psi(i) = C
		t0 = Tg(i)
	end
	if nargout == 1 then ...
		return
	#
	#
	t0    = 0d0
	C_a   = 0
	Psi_a = fill( 0d0, rowdim(Tg), rowdim(a) )
	for i = 1 to M begin
		function RHS_a1 = RHS_a(Fe, Fb, Vc, Tf, a)
		C_a = C_a + gaussq(function RHS_a1, t0, Tg(i), bound)
		Psi_a(i,:) = C_a
		t0 = Tg(i)
	end

end

end # blocking for automatic include
